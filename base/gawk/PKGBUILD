# $Id: PKGBUILD 100064 2010-11-20 03:46:49Z allan $
# Maintainer:
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=gawk
pkgver=3.1.8
pkgrel=2
pkgdesc="Gnu version of awk"
arch=(i686 x86_64)
url="http://www.gnu.org/directory/GNU/gawk.html"
license=('GPL')
groups=('base')
provides=('awk')
install=gawk.install
source=(ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz)
depends=('sh' 'glibc')
md5sums=('35937a0f83f0efe7a8c2dee635624784')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR=${pkgdir} install
  mv ${pkgdir}/usr/libexec ${pkgdir}/usr/lib
  install -dm755 ${pkgdir}/bin
  mv ${pkgdir}/usr/bin/gawk* ${pkgdir}/bin/
  mv ${pkgdir}/usr/bin/awk ${pkgdir}/bin/
  cd ${pkgdir}/usr/bin/
  ln -sf /bin/gawk gawk
}
