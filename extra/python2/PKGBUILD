# $Id: PKGBUILD 119810 2011-04-15 12:17:53Z stephane $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributer: Jason Chu <jason@archlinux.org>

pkgname=python2
pkgver=2.7.1
pkgrel=9
_pybasever=2.7
pkgdesc="A high-level scripting language"
arch=('i686' 'x86_64')
license=('PSF')
url="http://www.python.org/"
depends=('db' 'bzip2' 'gdbm' 'openssl' 'zlib' 'expat' 'sqlite3' 'libffi')
makedepends=('tk')
optdepends=('tk: for IDLE')
conflicts=('python<3')
options=('!makeflags')
source=(http://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.bz2
        CVE-2011-1521.patch
        python-2.7.1-fix-decimal-in-turkish-locale.patch
        python-2.7-db51.patch)
sha1sums=('fbe1894322ff91b80726e269c97454f4129fc2a3'
         '31cdc76092d0f598289aaeb18e492874c981904d'
         'baf470682ae7d2b55caaa173696d08d3f468a569' 
         '9667a2a2f8594902b352793e649f78696a77bd13')

build() {
  cd "${srcdir}/Python-${pkgver}"

  patch -Np1 -i ../python-2.7-db51.patch

  # Fix urllib Security Vulnerability
  # http://blog.python.org/2011/04/urllib-security-vulnerability-fixed.html
  patch -Np1 -i ../CVE-2011-1521.patch

  # Fix "import decimal" in the Turkish locale
  # cf : https://bugzilla.redhat.com/show_bug.cgi?id=694928
  patch -Np1 -i ../python-2.7.1-fix-decimal-in-turkish-locale.patch

  # Temporary workaround for FS#22322
  # See http://bugs.python.org/issue10835 for upstream report
  sed -i "/progname =/s/python/python${_pybasever}/" Python/pythonrun.c

  # Enable built-in SQLite3 module to load extensions (fix FS#22122)
  sed -i "/SQLITE_OMIT_LOAD_EXTENSION/d" setup.py

  # Ensure that we are using the system copy of various libraries (expat, zlib and libffi),
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*

  export OPT="${CFLAGS}"
  ./configure --prefix=/usr --enable-shared --with-threads --enable-ipv6 \
              --enable-unicode=ucs4 --with-system-expat --with-system-ffi

  make
}

package() {
  cd "${srcdir}/Python-${pkgver}"
  make DESTDIR="${pkgdir}" altinstall maninstall

  ln -sf python${_pybasever}        "${pkgdir}/usr/bin/python2"
  ln -sf python${_pybasever}-config "${pkgdir}/usr/bin/python2-config"
  ln -sf python${_pybasever}.1      "${pkgdir}/usr/share/man/man1/python2.1"

  ln -sf ../../libpython${_pybasever}.so \
    "${pkgdir}/usr/lib/python${_pybasever}/config/libpython${_pybasever}.so"

  mv "${pkgdir}/usr/bin/smtpd.py" "${pkgdir}/usr/lib/python${_pybasever}/"

  # some useful "stuff"
  install -dm755 "${pkgdir}"/usr/lib/python${_pybasever}/Tools/{i18n,scripts}
  install -m755 Tools/i18n/{msgfmt,pygettext}.py \
    "${pkgdir}/usr/lib/python${_pybasever}/Tools/i18n/"
  install -m755 Tools/scripts/{README,*py} \
    "${pkgdir}/usr/lib/python${_pybasever}/Tools/scripts/"

  # fix conflicts with python
  mv "${pkgdir}"/usr/bin/idle{,2}
  mv "${pkgdir}"/usr/bin/pydoc{,2}
  mv "${pkgdir}"/usr/bin/2to3{,-2.7}

  # clean up #!s
  find "${pkgdir}/usr/lib/python${_pybasever}/" -name '*.py' | \
    xargs sed -i "s|#[ ]*![ ]*/usr/bin/env python$|#!/usr/bin/env python2|"

  # clean-up reference to build directory
  sed -i "s#${srcdir}/Python-${pkgver}:##" \
    "${pkgdir}/usr/lib/python${_pybasever}/config/Makefile"

  # license
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
