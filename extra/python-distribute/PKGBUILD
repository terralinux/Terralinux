# $Id: PKGBUILD 122277 2011-05-03 17:48:08Z stephane $
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Sebastien Binet <binet@farnsworth>
pkgbase=python-distribute
pkgname=('python-distribute' 'python2-distribute')
pkgver=0.6.16
pkgrel=1
pkgdesc="Easily build and distribute Python packages"
arch=('any')
license=('PSF')
url="http://pypi.python.org/pypi/distribute"
makedepends=('python' 'python2')
source=(http://pypi.python.org/packages/source/d/distribute/distribute-${pkgver}.tar.gz
        distribute-0.6.16-fix_deprecation_warnings.patch
        distribute-python2_and_3.patch)
sha1sums=('d31026d1b80f6340d4a85b9b45e5a5703d443104'
          'a20bb3f35a03847ebe02199f2ce1a1c3883ef171' 
          'bcdafbe9b74b4bc2fbb866a3383a2b40c8f72543')

build() {
   cd "${srcdir}"

   pushd distribute-${pkgver}
   patch -Np1 -i ../distribute-python2_and_3.patch
   patch -Np1 -i ../distribute-0.6.16-fix_deprecation_warnings.patch
   popd

   cp -a distribute-${pkgver}{,-python2}

   # Build python 3 module
   cd distribute-${pkgver}
   python setup.py build
   python setup.py test

   # Build python 2 module
   cd ../distribute-${pkgver}-python2
   python2 setup.py build
   python2 setup.py test
}

package_python-distribute() {
   depends=('python>=3.2')

   cd "${srcdir}/distribute-${pkgver}"
   python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-distribute() {
   depends=('python2>=2.7')
   provides=('setuptools=0.6c11')
   conflicts=('setuptools')

   cd "${srcdir}/distribute-${pkgver}-python2"
   python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
}
