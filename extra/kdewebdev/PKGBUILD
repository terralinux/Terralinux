# $Id: PKGBUILD 126546 2011-06-06 06:10:12Z andrea $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdewebdev
pkgname=('kdewebdev-kfilereplace'
         'kdewebdev-kimagemapeditor'
         'kdewebdev-klinkstatus'
         'kdewebdev-kommander')
pkgver=4.6.4
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdewebdev')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'ruby' 'tidyhtml' 'kdepim-runtime'
	'boost')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
sha1sums=('ef6b5142afd5b97ad8c1bffe0557e25477371344')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdewebdev-kfilereplace() {
	pkgdesc='Search & Replace Tool'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/utilities/kfilereplace/"
	install='kdewebdev.install'
	cd $srcdir/build/kfilereplace
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kfilereplace
	make DESTDIR=$pkgdir install
}

package_kdewebdev-kimagemapeditor() {
	pkgdesc='HTML Image Map Editor'
	depends=('kdebase-runtime')
	install='kdewebdev.install'
	cd $srcdir/build/kimagemapeditor
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kimagemapeditor
	make DESTDIR=$pkgdir install
}

package_kdewebdev-klinkstatus() {
	pkgdesc='Link Checker'
	depends=('kdepim-runtime' 'tidyhtml')
	install='kdewebdev.install'
	cd $srcdir/build/klinkstatus
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/klinkstatus
	make DESTDIR=$pkgdir install
}

package_kdewebdev-kommander() {
	pkgdesc='Executor for Kommander dialogs'
	depends=('kdebase-runtime')
	cd $srcdir/build/kommander
	make DESTDIR=$pkgdir install
}
