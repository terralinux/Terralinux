# Maintainer: Martin Lee <hellnest@terralinux.org>

pkgname=bitlbee
pkgver=3.0.3
pkgrel=2
pkgdesc='Brings instant messaging (XMPP, MSN, Yahoo!, AIM, ICQ, Twitter) to IRC'
arch=('i686' 'x86_64')
url='http://www.bitlbee.org/'
license=('GPL')
depends=('openssl' 'glib2' 'libotr')
optdepends=('xinetd: to run bitlbee through xinetd')
source=("http://get.bitlbee.org/src/${pkgname}-${pkgver}.tar.gz"
	'xinetd'
	'rc.d')
sha1sums=('4140eb7aaa2c6a39fa059d19f8fbaec0d7a1ebff'
          '5e0af27ba9cc4fe455e3381c75fc49a9326e2f17'
          'be09d29af3853825778331df451f16ca9e452d03')

backup=('etc/bitlbee/bitlbee.conf' 'etc/bitlbee/motd.txt' 'etc/xinetd.d/bitlbee')
install=install

build() { 
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	./configure --prefix=/usr \
		--etcdir=/etc/bitlbee \
		--pidfile=/var/run/bitlbee/bitlbee.pid \
		--ipcsocket=/var/run/bitlbee/bitlbee.sock \
		--systemdsystemunitdir=/lib/systemd/system \
		--ssl=openssl \
		--strip=0 \
		--otr=1 \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	install -o65 -g65 -dm770 "${pkgdir}"/var/lib/bitlbee
	install -Dm644 "${srcdir}"/xinetd "${pkgdir}"/etc/xinetd.d/bitlbee
	install -Dm755 "${srcdir}"/rc.d "${pkgdir}"/etc/rc.d/bitlbee

	make DESTDIR="${pkgdir}" install
	make DESTDIR="${pkgdir}" install-etc
	make DESTDIR="${pkgdir}" install-dev
}
