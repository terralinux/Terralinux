# $Id: PKGBUILD 109954 2011-02-14 13:17:18Z andrea $
# Maintainer:
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=libldap
pkgver=2.4.24
pkgrel=1
pkgdesc="Lightweight Directory Access Protocol (LDAP) client libraries"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.openldap.org/"
backup=(etc/openldap/ldap.conf)
depends=('libsasl' 'openssl')
makedepends=('tcp_wrappers' 'groff')
options=('!libtool')
source=("ftp://ftp.openldap.org/pub/OpenLDAP/openldap-release/openldap-${pkgver}.tgz"
        'ntlm.patch')
md5sums=('116fe1e23a7b67686d5e62274367e6c0'
         'dfa9eb6f2fd9d0a3dab0e1860923489e')

build() {
  cd ${srcdir}/openldap-${pkgver}

  patch -Np1 -i ${srcdir}/ntlm.patch

  ./configure --prefix=/usr \
    --libexecdir=/usr/sbin \
    --sysconfdir=/etc \
    --mandir=/usr/share/man \
    --localstatedir=/var/lib/openldap \
    --enable-crypt \
    --enable-dynamic \
    --with-threads \
    --enable-wrappers \
    --enable-spasswd \
    --with-cyrus-sasl \
    --disable-bdb \
    --disable-hdb

  cd include
  make

  cd ../libraries
  make depend
  make

  cd ../doc/man/man3
  make
  
  cd ../man5
  make
}

package() {
  cd ${srcdir}/openldap-${pkgver}

  cd include
  make DESTDIR=${pkgdir} install

  cd ../libraries
  make DESTDIR=${pkgdir} install

  cd ../doc/man/man3
  make DESTDIR=${pkgdir} install

  cd ../man5
  install -Dm644 ldap.conf.5.tmp \
    ${pkgdir}/usr/share/man/man5/ldap.conf.5
  
  # get rid of duplicate default conf files
  rm ${pkgdir}/etc/openldap/*.default

  ln -sf liblber.so ${pkgdir}/usr/lib/liblber.so.2
  ln -sf libldap.so ${pkgdir}/usr/lib/libldap.so.2

  install -Dm644 ${srcdir}/openldap-${pkgver}/LICENSE \
    ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
