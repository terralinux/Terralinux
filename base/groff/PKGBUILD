# $Id: PKGBUILD 104541 2011-01-03 22:33:16Z tpowa $
# Maintainer: judd <jvinet@zeroflux.org>
pkgname=groff
pkgver=1.21
pkgrel=1
pkgdesc="GNU troff text-formatting system"
arch=(i686 x86_64)
url="http://www.gnu.org/software/groff/groff.html"
license=('GPL')
depends=('perl' 'gcc-libs' 'texinfo')
makedepends=('netpbm' 'psutils' 'ghostscript')
optdepends=('netpbm: for use together with man -H command interaction in browsers'
            'psutils: for use together with man -H command interaction in browsers')
source=(ftp://ftp.gnu.org/gnu/groff/groff-$pkgver.tar.gz
        site.tmac)
options=('!makeflags')
install=groff.install
md5sums=('8b8cd29385b97616a0f0d96d0951c5bf'
         'a1fedafd7863b37968d32ad9ae1d8c13')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --without-x
  mkdir -p $pkgdir/usr
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make prefix=$pkgdir/usr install
	cd $pkgdir/usr/bin
	# needed for xman
	ln -s eqn geqn
	ln -s tbl gtbl
	ln -s soelim zsoelim
  rm -rf $pkgdir/usr/lib/groff/site-tmac
  # Fix some issues when encoding to utf8 man pages
  #  The output chars don't match keyboard chars...
  cat $srcdir/site.tmac >> \
    $pkgdir/usr/share/groff/site-tmac/man.local
  cat $srcdir/site.tmac >> \
    $pkgdir/usr/share/groff/site-tmac/mdoc.local
}
