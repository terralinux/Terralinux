# $Id: PKGBUILD 127099 2011-06-10 17:21:18Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdeedu
pkgname=('kdeedu-blinken'
         'kdeedu-cantor'
         'kdeedu-kalgebra'
         'kdeedu-kalzium'
         'kdeedu-kanagram'
         'kdeedu-kbruch'
         'kdeedu-kgeography'
         'kdeedu-khangman'
         'kdeedu-kig'
         'kdeedu-kiten'
         'kdeedu-klettres'
         'kdeedu-kmplot'
         'kdeedu-kstars'
         'kdeedu-ktouch'
         'kdeedu-kturtle'
         'kdeedu-kwordquiz'
         'kdeedu-libkdeedu'
         'kdeedu-marble'
         'kdeedu-parley'
         'kdeedu-rocs'
         'kdeedu-step')
pkgver=4.6.4
pkgrel=2
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'ocaml' 'facile' 'cfitsio' 'gpsd'
	'kdebase-runtime' 'libqalculate' 'gsl' 'boost' 'libindi' 'r' 'libspectre'
	'python2' 'avogadro')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2"
        'gpsd-2.96.patch'
        'remove-libkdeedu-libscience.patch')
sha1sums=('c58c2afbedccfa12d25e47202da995940cc9acb4'
          'fd6b6b81945d951bb6ad0bb735df33c11bb2008e'
          'cd4e18f2b30d42f6e2bcc518ad72fd49839fd8dc')

build() {
    cd ${srcdir}/${pkgbase}-${pkgver}
    patch -Np1 -i ${srcdir}/gpsd-2.96.patch
    patch -p1 -i ${srcdir}/remove-libkdeedu-libscience.patch

    cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=OFF \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdeedu-blinken() {
	pkgdesc='A memory enhancement game'
	depends=('kdebase-runtime' 'kdeedu-libkdeedu')
	install='kdeedu.install'
	cd $srcdir/build/blinken
	make DESTDIR=$pkgdir install
	cd $srcdir/build/blinken/doc
	make DESTDIR=$pkgdir install
}

package_kdeedu-cantor() {
	pkgdesc='KDE Frontend to Mathematical Software'
	depends=('kdeedu-libkdeedu' 'libspectre')
	optdepends=('kdeedu-kalgebra: backend'
		'maxima: backend'
		'r: backend')
	install='kdeedu.install'
	cd $srcdir/build/cantor
	make DESTDIR=$pkgdir install
	cd $srcdir/build/cantor/doc
	make DESTDIR=$pkgdir install
}

package_kdeedu-kalgebra() {
	pkgdesc='Math Expression Solver and Plotter'
	depends=('kdebase-runtime')
	install='kdeedu.install'
	cd $srcdir/build/kalgebra
	make DESTDIR=$pkgdir install
	cd $srcdir/build/kalgebra/doc
	make DESTDIR=$pkgdir install
}

package_kdeedu-kalzium() {
	pkgdesc='KDE Periodic Table of Elements'
	depends=('kdebase-runtime'  'kdeedu-libkdeedu' 'avogadro')
	install='kdeedu.install'
	cd $srcdir/build/kalzium
	make DESTDIR=$pkgdir install
	cd $srcdir/build/kalzium/doc
	make DESTDIR=$pkgdir install
}

package_kdeedu-kanagram() {
	pkgdesc='KDE Letter Order Game'
	depends=('kdebase-runtime' 'kdeedu-libkdeedu')
	install='kdeedu.install'
	cd $srcdir/build/kanagram
	make DESTDIR=$pkgdir install
	cd $srcdir/build/kanagram/doc
	make DESTDIR=$pkgdir install
}

package_kdeedu-kbruch() {
	pkgdesc='Practice exercises with fractions'
	depends=('kdelibs' 'kdebase-runtime')
	install='kdeedu.install'
	cd $srcdir/build/kbruch
	make DESTDIR=$pkgdir install
	cd $srcdir/build/kbruch/doc
	make DESTDIR=$pkgdir install
}

package_kdeedu-kgeography() {
	pkgdesc='A Geography Learning Program'
	depends=('kdebase-runtime')
	install='kdeedu.install'
	cd $srcdir/build/kgeography
	make DESTDIR=$pkgdir install
	cd $srcdir/build/kgeography/doc
	make DESTDIR=$pkgdir install
}

package_kdeedu-khangman() {
	pkgdesc='KDE Hangman Game'
	depends=('kdebase-runtime' 'kdeedu-libkdeedu')
	install='kdeedu.install'
	cd $srcdir/build/khangman
	make DESTDIR=$pkgdir install
	cd $srcdir/build/khangman/doc
	make DESTDIR=$pkgdir install
}

package_kdeedu-kig() {
	pkgdesc='Explore Geometric Constructions'
	depends=('kdebase-runtime' 'boost-libs' 'python2')
	install='kdeedu.install'
	cd $srcdir/build/kig
	make DESTDIR=$pkgdir install
	cd $srcdir/build/kig/doc
	make DESTDIR=$pkgdir install

	# Use the python2 executable
	sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' ${pkgdir}/usr/bin/pykig.py
}

package_kdeedu-kiten() {
	pkgdesc='Japanese Reference and Study Tool'
	depends=('kdebase-runtime')
	install='kdeedu.install'
	cd $srcdir/build/kiten
	make DESTDIR=$pkgdir install
	cd $srcdir/build/kiten/doc
	make DESTDIR=$pkgdir install
}

package_kdeedu-klettres() {
	pkgdesc='a KDE program to learn the alphabet'
	depends=('kdebase-runtime')
	install='kdeedu.install'
	cd $srcdir/build/klettres
	make DESTDIR=$pkgdir install
	cd $srcdir/build/klettres/doc
	make DESTDIR=$pkgdir install
}

package_kdeedu-kmplot() {
	pkgdesc='Function Plotter'
	depends=('kdebase-runtime')
	install='kdeedu.install'
	cd $srcdir/build/kmplot
	make DESTDIR=$pkgdir install
	cd $srcdir/build/kmplot/doc
	make DESTDIR=$pkgdir install
}

package_kdeedu-kstars() {
	pkgdesc='Desktop Planetarium'
	depends=('kdebase-runtime' 'libindi')
	install='kdeedu.install'
	cd $srcdir/build/kstars
	make DESTDIR=$pkgdir install
	cd $srcdir/build/kstars/doc
	make DESTDIR=$pkgdir install
}

package_kdeedu-ktouch() {
	pkgdesc='Touch Typing Tutor'
	depends=('kdebase-runtime')
	install='kdeedu.install'
	cd $srcdir/build/ktouch
	make DESTDIR=$pkgdir install
	cd $srcdir/build/ktouch/doc
	make DESTDIR=$pkgdir install
}

package_kdeedu-kturtle() {
	pkgdesc='Educational Programming Environment'
	depends=('kdebase-runtime')
	install='kdeedu.install'
	cd $srcdir/build/kturtle
	make DESTDIR=$pkgdir install
	cd $srcdir/build/kturtle/doc
	make DESTDIR=$pkgdir install
}

package_kdeedu-kwordquiz() {
	pkgdesc='A flashcard and vocabulary learning program'
	depends=('kdebase-runtime' 'kdeedu-libkdeedu')
	install='kdeedu.install'
	cd $srcdir/build/kwordquiz
	make DESTDIR=$pkgdir install
	cd $srcdir/build/kwordquiz/doc
	make DESTDIR=$pkgdir install
}

package_kdeedu-libkdeedu() {
	pkgdesc='Support library for KDE Educational Software'
	groups=()
	depends=('kdelibs')
	install='kdeedu.install'
    replaces=('kdeedu-data')
    conflicts=('kdeedu-data')
	cd $srcdir/build/libkdeedu
	make DESTDIR=$pkgdir install
}

package_kdeedu-marble() {
	pkgdesc='Desktop Globe'
	depends=('kdebase-runtime' 'gpsd')
	install='kdeedu.install'
	cd $srcdir/build/marble
	make DESTDIR=$pkgdir install
	cd $srcdir/build/marble/doc
	make DESTDIR=$pkgdir install
}

package_kdeedu-parley() {
	pkgdesc='Vocabulary Trainer'
	depends=('kdebase-runtime' 'kdeedu-libkdeedu' 'kdebindings-python')
	install='kdeedu.install'
	cd $srcdir/build/parley
	make DESTDIR=$pkgdir install
	cd $srcdir/build/parley/doc
	make DESTDIR=$pkgdir install
}

package_kdeedu-rocs() {
	pkgdesc='Graph Theory Tool for Professors and Students.'
	depends=('kdebase-runtime' 'kdeedu-libkdeedu')
	install='kdeedu.install'
	cd $srcdir/build/rocs
	make DESTDIR=$pkgdir install
	cd $srcdir/build/rocs/doc
	make DESTDIR=$pkgdir install
}

package_kdeedu-step() {
	pkgdesc='Simulate physics experiments'
	depends=('kdebase-runtime' 'gsl' 'libqalculate')
	install='kdeedu.install'
	cd $srcdir/build/step
	make DESTDIR=$pkgdir install
	cd $srcdir/build/step/doc
	make DESTDIR=$pkgdir install
}
