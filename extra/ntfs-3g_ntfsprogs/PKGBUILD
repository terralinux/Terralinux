# Maintainer: Martin Lee <hellnest@terralinux.org>

pkgbase=ntfs-3g_ntfsprogs
pkgname=('ntfs-3g' 'ntfsprogs')
pkgver=2011.4.12
pkgrel=1
url="http://www.tuxera.com"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('glibc' 'fuse')
makedepends=('pkgconfig')
options=('!libtool')
source=(http://tuxera.com/opensource/${pkgbase}-${pkgver}.tgz
        25-ntfs-config-write-policy.fdi)
sha1sums=('a4340ae611a3f26089f7d5601a86a5847517bff7'
          '200029f2999a2c284fd30ae25734abf6459c3501')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ac_cv_path_LDCONFIG=/bin/true ./configure --prefix=/usr \
    --with-fuse=external --disable-static --disable-gnome-vfs \
      --mandir=/usr/share/man
  make
}

package_ntfs-3g() {
  pkgdesc="Stable read and write NTFS driver"

  for DIR in src libntfs-3g include; do
    cd "${srcdir}/${pkgbase}-${pkgver}/${DIR}"
    make DESTDIR="${pkgdir}" install
  done

  ln -s /bin/ntfs-3g "${pkgdir}/sbin/mount.ntfs"
  install -m755 -d "${pkgdir}/usr/share/hal/fdi/policy/10osvendor"
  install -m644 "${srcdir}/25-ntfs-config-write-policy.fdi" "${pkgdir}/usr/share/hal/fdi/policy/10osvendor/"
}

package_ntfsprogs() {
  pkgdesc="NTFS filesystem utilities"
  depends=('util-linux' 'ntfs-3g')

  cd "${srcdir}/${pkgbase}-${pkgver}/ntfsprogs"
  make DESTDIR="${pkgdir}" install
}
