# Maintainer: Martin Lee <hellnest@terralinux.org>

pkgbase="sqlite3"
pkgname=('sqlite3' 'sqlite3-tcl' 'sqlite3-doc') 
_amalgamationver=3070700
_amalgamationver2=${_amalgamationver/00/}
_docver=${_amalgamationver}	#3070700
pkgver=${_amalgamationver2//0/.}
pkgrel=1
pkgdesc="A C library that implements an SQL database engine"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.sqlite.org/"
makedepends=('tcl' 'readline>=6.0.00')
source=( # tarball containing the amalgamation for SQLite 3.7.5 together with a configure script and makefile for building it; includes now also the Tcl Extension Architecture (TEA)
	http://www.sqlite.org/sqlite-autoconf-$_amalgamationver.tar.gz
	#http://www.sqlite.org/sqlite-doc-${_amalgamationver}.zip
	http://www.sqlite.org/sqlite-doc-${_docver}.zip
	license.txt)
options=('!libtool' '!emptydirs')
md5sums=('a98df57053adf4c132b6b7ab59e9f333'
         '91a9a3327b46d6d0a1341e9c95cba1e9'
         'c1cdbc5544034d9012e421e75a5e4890')

build() {
  export CFLAGS="$CFLAGS -DSQLITE_ENABLE_FTS3=1 -DSQLITE_ENABLE_COLUMN_METADATA=1 -DSQLITE_ENABLE_UNLOCK_NOTIFY -DSQLITE_SECURE_DELETE"

  # build sqlite
  cd "$srcdir"/sqlite-autoconf-$_amalgamationver
  ./configure --prefix=/usr \
	--disable-static
  make
  
  # build the tcl extension    
  cd "$srcdir"/sqlite-autoconf-$_amalgamationver/tea
  ./configure --prefix=/usr \
	--with-system-sqlite
  make

}

package_sqlite3() {
 
 pkgdesc="A C library that implements an SQL database engine"
 depends=('readline>=6.0.00')
 conflicts=('sqlite')
 replaces=('sqlite')
 
  cd ${srcdir}/sqlite-autoconf-$_amalgamationver
  make DESTDIR=${pkgdir} install
  
  # license
  install -D -m644 ${srcdir}/license.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}

package_sqlite3-tcl() {
 
 pkgdesc="sqlite3 Tcl Extension Architecture (TEA)"
 depends=('sqlite3>=3.7.5')

  cd ${srcdir}/sqlite-autoconf-$_amalgamationver/tea
  make DESTDIR=${pkgdir} install
}

package_sqlite3-doc() {
 pkgdesc="most of the static HTML files that comprise this website, including all of the SQL Syntax and the C/C++ interface specs and other miscellaneous documentation"
 #arch=('any') - not yet supported

  #cd ${srcdir}/sqlite-doc-${_amalgamationver}
  cd ${srcdir}/sqlite-doc-${_docver}
  mkdir -p ${pkgdir}/usr/share/doc/${pkgbase}
  cp -R *  ${pkgdir}/usr/share/doc/${pkgbase}/
}
