# $Id: PKGBUILD 117304 2011-03-30 19:38:26Z ibiru $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: onestep_ua <onestep@ukr.net>

pkgname=polkit-gnome
pkgver=0.101
pkgrel=2
pkgdesc="PolicyKit integration for the GNOME desktop"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/PolicyKit"
license=('LGPL')
depends=('polkit>=0.101' 'gtk3')
makedepends=('intltool' 'gobject-introspection')
options=(!libtool)
source=(http://hal.freedesktop.org/releases/${pkgname}-${pkgver}.tar.bz2
        polkit-gnome-authentication-agent-1.desktop)
md5sums=('da6aaff473ed80f8958fd6f67a59defe'
         '5668cffbfd031452c86db8adceba79b5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/polkit-gnome \
      --disable-static --with-gtk=3.0
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/polkit-gnome-authentication-agent-1.desktop" \
      "${pkgdir}/etc/xdg/autostart/polkit-gnome-authentication-agent-1.desktop"
}
