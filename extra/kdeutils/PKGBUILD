# $Id: PKGBUILD 121105 2011-04-29 00:28:11Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdeutils
pkgname=('kdeutils-ark'
         'kdeutils-filelight'
         'kdeutils-kcalc'
         'kdeutils-kcharselect'
         'kdeutils-kdf'
         'kdeutils-kfloppy'
         'kdeutils-kgpg'
         'kdeutils-kremotecontrol'
         'kdeutils-ktimer'
         'kdeutils-kwallet'
         'kdeutils-printer-applet'
         'kdeutils-superkaramba'
         'kdeutils-sweeper')
pkgver=4.6.3
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeutils')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdebase-lib' 'kdebase-workspace'
	'kdebindings-python' 'system-config-printer-common' 'libarchive' 'qimageblitz'
	'qjson')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
sha1sums=('1aa735c03689ef49b949278ee31d0d8e8536b7d6')

build() {
	cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdeutils-ark() {
	pkgdesc='Archiving Tool'
	depends=('kdebase-runtime' 'kdebase-lib' 'libarchive')
	optdepends=('p7zip' 'zip' 'unzip' 'unrar')
	url="http://kde.org/applications/utilities/ark/"
	cd $srcdir/build/ark
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/ark
	make DESTDIR=$pkgdir install
}

package_kdeutils-filelight() {
	pkgdesc='View disk usage information'
	depends=('kdebase-runtime' 'qimageblitz')
	replaces=('filelight')
	conflicts=('filelight')
	install='kdeutils.install'
	url="http://methylblue.com/filelight/"
	cd $srcdir/build/filelight
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/filelight
	make DESTDIR=$pkgdir install
}

package_kdeutils-kcalc() {
	pkgdesc='Scientific Calculator'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/utilities/kcalc/"
	cd $srcdir/build/kcalc
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kcalc
	make DESTDIR=$pkgdir install
}

package_kdeutils-kcharselect() {
	pkgdesc='Character Selector'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/utilities/kcharselect/"
	cd $srcdir/build/kcharselect
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kcharselect
	make DESTDIR=$pkgdir install
}

package_kdeutils-kdf() {
	pkgdesc='View Disk Usage'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/system/kdiskfree/"
	install='kdeutils.install'
	cd $srcdir/build/kdf
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kdf
	make DESTDIR=$pkgdir install
}

package_kdeutils-kfloppy() {
	pkgdesc='Floppy Formatter'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/utilities/kfloppy/"
	install='kdeutils.install'
	cd $srcdir/build/kfloppy
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kfloppy
	make DESTDIR=$pkgdir install
}

package_kdeutils-kgpg() {
	pkgdesc='A GnuPG frontend'
	depends=('kdepim-runtime' 'kde-agent')
	url="http://kde.org/applications/utilities/kgpg"
	install='kdeutils.install'
	cd $srcdir/build/kgpg
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kgpg
	make DESTDIR=$pkgdir install
}

package_kdeutils-kremotecontrol() {
	pkgdesc='Configure your remote controls for use with applications'
	replaces=('kdeutils-kdelirc')
	conflicts=('kdeutils-kdelirc')
	depends=('kdebase-workspace')
	url="http://kde.org/applications/utilities/kremotecontrol"
	install='kdeutils.install'
	cd $srcdir/build/kremotecontrol
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kcontrol/kremotecontrol
	make DESTDIR=$pkgdir install
}

package_kdeutils-ktimer() {
	pkgdesc='Countdown Launcher'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/utilities/ktimer"
	install='kdeutils.install'
	cd $srcdir/build/ktimer
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/ktimer
	make DESTDIR=$pkgdir install
}

package_kdeutils-kwallet() {
	pkgdesc='Wallet Management Tool'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/system/kwalletmanager/"
	install='kdeutils.install'
	cd $srcdir/build/kwallet
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kwallet
	make DESTDIR=$pkgdir install
}

package_kdeutils-printer-applet() {
	pkgdesc='System tray icon for managing print jobs'
	depends=('kdebase-runtime' 'kdebindings-python' 'system-config-printer-common')
	url="http://kde.org/applications/system/printerapplet/"
	cd $srcdir/build/printer-applet
	make DESTDIR=$pkgdir install

        # Use the python2 executable
        find "${pkgdir}" -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}

package_kdeutils-superkaramba() {
	pkgdesc='An engine for cool desktop eyecandy'
	depends=('kdebase-runtime' 'kdebindings-python' 'qimageblitz')
	url="http://kde.org/applications/utilities/superkaramba"
	install='kdeutils.install'
	cd $srcdir/build/superkaramba
	make DESTDIR=$pkgdir install
}

package_kdeutils-sweeper() {
	pkgdesc='System Cleaner'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/utilities/sweeper/"
	cd $srcdir/build/sweeper
	make DESTDIR=$pkgdir install
}
sha1sums=('1aa735c03689ef49b949278ee31d0d8e8536b7d6')
sha1sums=('1aa735c03689ef49b949278ee31d0d8e8536b7d6')
