# $Id: PKGBUILD 121093 2011-04-29 00:27:32Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdegraphics
pkgname=('kdegraphics-gwenview'
         'kdegraphics-kamera'
         'kdegraphics-kcolorchooser'
         'kdegraphics-kgamma'
         'kdegraphics-kolourpaint'
         'kdegraphics-kruler'
         'kdegraphics-ksnapshot'
         'kdegraphics-libs'
         'kdegraphics-okular')
pkgver=4.6.3
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'qimageblitz' 'sane' 'lcms'
        'libxxf86vm' 'poppler-qt' 'libspectre' 'chmlib' 'djvulibre'
        'ebook-tools' 'kdebase-runtime')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
sha1sums=('c16dc9c55e7459418fa9887aa532ea0a269e7179')

build() {
	cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdegraphics-gwenview() {
	pkgdesc='A simple image viewer'
	depends=('kdebase-runtime' 'kdegraphics-libs')
	url="http://kde.org/applications/graphics/gwenview/"
	replaces=('gwenview' 'gwenview-i18n')
	install='kdegraphics.install'
	cd $srcdir/build/gwenview
	make DESTDIR=$pkgdir install
	cd $srcdir/build/gwenview/doc
	make DESTDIR=$pkgdir install
}

package_kdegraphics-kamera() {
	pkgdesc='Configure Kamera'
	depends=('kdelibs' 'libgphoto2')
	cd $srcdir/build/kamera
	make DESTDIR=$pkgdir install
	cd $srcdir/build/kamera/doc
	make DESTDIR=$pkgdir install
}

package_kdegraphics-kcolorchooser() {
	pkgdesc='Color Chooser'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/graphics/kcolorchooser/"
	install='kdegraphics.install'
	cd $srcdir/build/kcolorchooser
	make DESTDIR=$pkgdir install
}

package_kdegraphics-kgamma() {
	pkgdesc='A monitor calibration tool'
	# note on libxxf86vm:
	#       not detected by namcap because libgl depends on it
	#       but nvidia providing libgl does not depend on libxxf86vm
	depends=('kdebase-runtime' 'libxxf86vm')
	install='kdegraphics.install'
	cd $srcdir/build/kgamma
	make DESTDIR=$pkgdir install
	cd $srcdir/build/kgamma/doc
	make DESTDIR=$pkgdir install
}

package_kdegraphics-kolourpaint() {
	pkgdesc='Paint Program'
	depends=('kdebase-runtime' 'qimageblitz' 'kdegraphics-libs')
	url="http://kde.org/applications/graphics/kolourpaint/"
	install='kdegraphics.install'
	cd $srcdir/build/kolourpaint
	make DESTDIR=$pkgdir install
	cd $srcdir/build/kolourpaint/doc
	make DESTDIR=$pkgdir install
}

package_kdegraphics-kruler() {
	pkgdesc='Screen Ruler'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/graphics/kruler/"
	install='kdegraphics.install'
	cd $srcdir/build/kruler
	make DESTDIR=$pkgdir install
	cd $srcdir/build/kruler/doc
	make DESTDIR=$pkgdir install
}

package_kdegraphics-ksnapshot() {
	pkgdesc='Screen Capture Program'
	depends=('kdebase-runtime' 'kdegraphics-libs')
	url="http://kde.org/applications/graphics/ksnapshot/"
	install='kdegraphics.install'
	cd $srcdir/build/ksnapshot
	make DESTDIR=$pkgdir install
	cd $srcdir/build/ksnapshot/doc
	make DESTDIR=$pkgdir install
}

package_kdegraphics-libs() {
	pkgdesc='Support library for KDE Graphics Software'
	groups=()
	depends=('kdelibs' 'sane' 'lcms')
	conflicts=('kdegraphics-ksaneplugin' 'kdegraphics-strigi-analyzer'
                   'kdegraphics-svgpart' 'kdegraphics-thumbnailers')
	replaces=('libkexiv2' 'libkdcraw' 'kdegraphics-ksaneplugin'
                  'kdegraphics-strigi-analyzer' 'kdegraphics-svgpart' 'kdegraphics-thumbnailers')
	install='kdegraphics.install'
	for i in libs ksaneplugin strigi-analyzer svgpart thumbnailers; do
		cd $srcdir/build/${i}
		make DESTDIR=$pkgdir install
	done
	install -D -m644 $srcdir/${pkgbase}-${pkgver}/cmake/modules/FindKSane.cmake \
		$pkgdir/usr/share/apps/cmake/modules/FindKSane.cmake
}

package_kdegraphics-okular() {
	pkgdesc='Document Viewer'
	depends=('kdebase-runtime' 'qimageblitz' 'poppler-qt' 'chmlib' 'djvulibre'
		'ebook-tools' 'libspectre')
	url="http://kde.org/applications/graphics/okular/"
	install='kdegraphics.install'
	cd $srcdir/build/okular
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/okular
	make DESTDIR=$pkgdir install
}
