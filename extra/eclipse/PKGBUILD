# Maintainer: Martin Lee <hellnest.fuah@gmail.com

pkgname=eclipse
pkgver=3.7
_internal_pkgver=3.7
pkgrel=1
_date=201106131736
pkgdesc="An IDE for Java and other languages"
arch=('i686' 'x86_64')
url="http://eclipse.org"
depends=('java-environment' 'gtk2' 'unzip' 'xulrunner')
install=${pkgname}.install
makedepends=('zip')
license=("EPL/1.1")
source=("ftp://ftp.osuosl.org/pub/eclipse/eclipse/downloads/drops/R-${pkgver}-${_date}/${pkgname}-SDK-${pkgver}-linux-gtk.tar.gz"
	'eclipse.svg' 'eclipse.sh' 'eclipse.desktop'
	'eclipse.ini.patch')

package() {
  cd "${srcdir}/eclipse"

  # patch to increase default memory limits
  patch -Np1 -i "${srcdir}/eclipse.ini.patch"

  # install eclipse
  install -m755 -d "${pkgdir}/usr/share"
  mv "${srcdir}/eclipse" "${pkgdir}/usr/share/"

  # install misc
  install -d ${pkgdir}/usr/bin ${pkgdir}/usr/share/applications \
    ${pkgdir}/usr/share/icons/hicolor/{16x16,32x32,48x48}/apps
  install -m755 "${srcdir}/eclipse.sh" "${pkgdir}/usr/bin/eclipse"
  install -m644 "${srcdir}/eclipse.desktop" "${pkgdir}/usr/share/applications/"
  ln -s /usr/share/eclipse/plugins/org.eclipse.sdk_${_internal_pkgver}.v${_date}/eclipse.png \
    "${pkgdir}/usr/share/icons/hicolor/16x16/apps/eclipse.png"
  ln -s /usr/share/eclipse/plugins/org.eclipse.sdk_${_internal_pkgver}.v${_date}/eclipse32.png \
    "${pkgdir}/usr/share/icons/hicolor/32x32/apps/eclipse.png"
  ln -s /usr/share/eclipse/plugins/org.eclipse.sdk_${_internal_pkgver}.v${_date}/eclipse48.png \
    "${pkgdir}/usr/share/icons/hicolor/48x48/apps/eclipse.png"

  # install icon
  install -Dm644 ${srcdir}/eclipse.svg \
    ${pkgdir}/usr/share/icons/hicolor/scalable/apps/eclipse.svg

  # fix python 2.7 path
  sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' \
    ${pkgdir}/usr/share/eclipse/plugins/org.apache.ant_1.7.1.v20100518-1145/bin/runant.py
}

