# $Id: PKGBUILD 106454 2011-01-17 12:49:41Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=pcre
pkgver=8.12
pkgrel=1
pkgdesc="A library that implements Perl 5-style regular expressions"
arch=('i686' 'x86_64')
url="http://www.pcre.org/"
license=('BSD')
depends=('gcc-libs')
options=('!libtool')
source=(ftp://ftp.csx.cam.ac.uk/pub/software/programming/pcre/${pkgname}-${pkgver}.tar.bz2)
md5sums=('f14a9fef3c92f3fc6c5ac92d7a2c7eb3')
sha1sums=('2219b372bff53ee29a7e44ecf5977ad15df01cea')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  [ "${CARCH}" = "x86_64" ] && export CFLAGS="${CFLAGS} -fPIC"
  ./configure --prefix=/usr --enable-utf8 --enable-unicode-properties
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  # grep uses pcre, so we need the libs in /lib
  install -dm755 "${pkgdir}"/lib
  mv "${pkgdir}"/usr/lib/libpcre.so.* "${pkgdir}"/lib/
  ln -sf /lib/libpcre.so.0 "${pkgdir}"/usr/lib/libpcre.so

  install -Dm644 LICENCE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
