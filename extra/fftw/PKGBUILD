# $Id: PKGBUILD 110392 2011-02-19 07:05:55Z allan $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=fftw
pkgver=3.2.2
pkgrel=2
pkgdesc="A library for computing the discrete Fourier transform (DFT)"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.fftw.org/"
depends=('glibc' 'bash')
makedepends=('gcc-fortran')
options=(!libtool)
source=(http://www.fftw.org/${pkgname}-${pkgver}.tar.gz)
install=fftw.install

# notes:
# http://www.fftw.org/fftw2_doc/fftw_6.html#SEC69
# http://www.fftw.org/faq/section2.html#singleprec
# http://www.fftw.org/fftw3_doc/Precision.html#Precision

md5sums=('b616e5c91218cc778b5aa735fefb61ae')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # use upstream default CFLAGS while keeping our -march/-mtune
  CFLAGS+=" -O3 -fomit-frame-pointer -malign-double -fstrict-aliasing -ffast-math"

  CONFIGURE="./configure F77=gfortran --prefix=/usr \
                 --enable-shared --enable-threads"

  # build & install double precision
  $CONFIGURE --enable-sse2
  make
  make DESTDIR=${pkgdir} install
  make clean 

  # build & install long double precission
  $CONFIGURE --enable-long-double
  make
  make DESTDIR=${pkgdir} install
  make clean

  # build & install single precision
  $CONFIGURE --enable-float --enable-sse
  make
  make DESTDIR=${pkgdir} install
}
