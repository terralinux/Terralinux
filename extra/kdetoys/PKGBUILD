# $Id: PKGBUILD 126544 2011-06-06 06:10:05Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdetoys
pkgname=('kdetoys-amor'
         'kdetoys-kteatime'
         'kdetoys-ktux')
pkgver=4.6.4
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdetoys')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdebase-workspace')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
sha1sums=('12a0228010a1720649807cb8efe71f572bd24af3')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdetoys-amor() {
	pkgdesc='On-Screen Creature'
	depends=('kdebase-runtime')
	install='kdetoys.install'
	cd $srcdir/build/amor
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/amor
	make DESTDIR=$pkgdir install
}

package_kdetoys-kteatime() {
	pkgdesc='Tea Cooker'
	depends=('kdebase-runtime')
	install='kdetoys.install'
	cd $srcdir/build/kteatime
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kteatime
	make DESTDIR=$pkgdir install
}

package_kdetoys-ktux() {
	pkgdesc='KTux'
	depends=('kdebase-workspace' )
	install='kdetoys.install'
	cd $srcdir/build/ktux
	make DESTDIR=$pkgdir install
}
