# Maintainer: Martin Lee <hellnest@terralinux.org>

pkgname=cairo
pkgver=1.10.2
_ubver=1.10.2-2ubuntu2
pkgrel=5
pkgdesc="Cairo vector graphics library, with Ubuntu's LCD rendering patches"
arch=(i686 x86_64)
url="https://launchpad.net/ubuntu/natty/+source/cairo"
license=('LGPL' 'MPL')
depends=('libpng>=1.4.0' 'libxrender' 'fontconfig>=2.8.0' 'libxft' 'pixman>=0.16.6' 'xcb-util>=0.3.6')
makedepends=('pkgconfig')
provides=("cairo=$pkgver" "cairo-tee=$pkgver")
options=('!libtool')
source=(http://cairographics.org/releases/cairo-$pkgver.tar.gz
        http://archive.ubuntu.com/ubuntu/pool/main/c/cairo/cairo_$_ubver.debian.tar.gz
        cairo-respect-fontconfig.patch)

md5sums=('f101a9e88b783337b20b2e26dfd26d5f'
         'c28ce4e75bf13c2a10727b2af1385059'
         '79f7c141c49f3d65ab308cc706d50914')

build() {
  cd $srcdir/cairo-$pkgver

  for _f in $(cat $srcdir/debian/patches/series) ; do    
    patch -Np1 -i $srcdir/debian/patches/$_f    
  done

  patch -Np1 -i $srcdir/cairo-respect-fontconfig.patch
  
  ./configure --prefix=/usr --sysconfdir=/etc \
	--localstatedir=/var --disable-static \
        --enable-tee
  make
}

package() {
  cd $srcdir/cairo-$pkgver

  make DESTDIR=$pkgdir install

  # Docs
  # Excluding debian/changelog temporarily
  install -d $pkgdir/usr/share/doc/cairo/
  install -m644 -t $pkgdir/usr/share/doc/cairo/ NEWS README
}
