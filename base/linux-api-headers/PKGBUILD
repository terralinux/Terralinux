# $Id: PKGBUILD lee $
# Maintainer: Martin Lee <hellnest@terralinux.org

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc

pkgname=linux-api-headers
pkgver=2.6.39.1
_zencommit=6d6d304
pkgrel=1
pkgdesc="Kernel headers sanitized for use in userspace"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/libc"
license=('GPL2')
provides=("kernel-headers=${pkgver}")
conflicts=('kernel-headers')
replaces=('kernel-headers')
_srcname=zen-stable-$_zencommit
source=(http://git.zen-kernel.org/zen-stable/snapshot/$_srcname.tar.bz2)

build() {
  cd ${srcdir}/${_srcname}
  
  make mrproper
  make headers_check
}

package() {
  cd ${srcdir}/${_srcname}
  make INSTALL_HDR_PATH=${pkgdir}/usr headers_install

  # use headers from libdrm
  rm -rf ${pkgdir}/usr/include/drm
  
  # clean-up unnecessary files generated during install
  find ${pkgdir} -name .install -or -name ..install.cmd | xargs rm -f
}

sha256sums=('8eaa158004ba30bd02b345455447f1c625de90e8389c3f296f8e1b1d5c73334a')
