# $Id: PKGBUILD 102128 2010-12-06 05:33:18Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=texinfo
pkgver=4.13a
pkgrel=5
pkgdesc="Utilities to work with and produce manuals, ASCII text, and on-line documentation from a single source file"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/texinfo/"
license=('GPL3')
groups=('base')
depends=('ncurses' 'findutils' 'gzip')
install=texinfo.install
source=(ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('71ba711519209b5fb583fed2b3d86fcb')

build() {
  cd ${srcdir}/${pkgname}-4.13
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-4.13
  make DESTDIR=${pkgdir} install

  # fix this here as it prevent make from trying to rebuild manual pages
  sed -i 's#A-z#A-Za-z#' ${pkgdir}/usr/bin/texi2dvi
}
