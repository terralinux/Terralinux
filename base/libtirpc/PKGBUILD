# $Id: PKGBUILD 121179 2011-04-29 06:50:46Z stephane $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=libtirpc
pkgver=0.2.1
pkgrel=3
pkgdesc="Transport Independent RPC library (SunRPC replacement)"
arch=(i686 x86_64)
depends=('glibc' 'libgssglue' 'krb5')
url="http://libtirpc.sourceforge.net/"
license=('GPL2')
source=(http://downloads.sourceforge.net/sourceforge/libtirpc/${pkgname}-${pkgver}.tar.bz2
       libtirpc-0.2.1-fortify.patch)
backup=('etc/netconfig')
options=(!libtool)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # fix http://bugs.gentoo.org/293593
  # https://bugs.archlinux.org/task/20082
  patch -Np1 -i ../libtirpc-0.2.1-fortify.patch
  ./configure --prefix=/usr --enable-gss
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # install netconfig
  install -D -m644 doc/etc_netconfig "${pkgdir}"/etc/netconfig
}
md5sums=('d77eb15f464bf9d6e66259eaf78b2a4e'
         '2e5c067f1651469dfbbdc91d3c9c60e8')
