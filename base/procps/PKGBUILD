# $Id: PKGBUILD 102904 2010-12-12 23:53:18Z eric $
# Maintainer: Martin Lee <hellnest@terralinux.org>

pkgname=procps
pkgver=3.2.8
pkgrel=3
pkgdesc="Utilities for monitoring your system and processes on your system"
arch=('i686' 'x86_64')
url="http://procps.sourceforge.net/"
license=('GPL' 'LGPL')
groups=('base')
depends=('ncurses')
backup=('etc/sysctl.conf')
source=(http://procps.sourceforge.net/procps-${pkgver}.tar.gz sysctl.conf\
        groff-top-manpage.patch            procps-3.2.7-free-hlmem.patch\
        procps-3.2.7-longcmd.patch         procps-3.2.7-ps-man-fmt.patch\
        procps-3.2.7-psman.patch           procps-3.2.7-slabtop-once.patch\
        procps-3.2.7-top-clrscr.patch      procps-3.2.7-top-cpu0.patch\
        procps-3.2.7-top-env-cpuloop.patch procps-3.2.7-top-manpage.patch\
        procps-3.2.7-top-remcpu.patch      procps-3.2.7-top-sorthigh.patch\
        procps-3.2.7-watch-unicode.patch   procps-3.2.7-w-best.patch\
        procps-3.2.8+gmake-3.82.patch      procps-3.2.8-setlocale.patch\
        procps-3.2.8-threads.patch         procps-3.2.8-linux-ver-init.patch)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../groff-top-manpage.patch
  patch -p1 < ../procps-3.2.7-top-manpage.patch
  patch -p1 < ../procps-3.2.7-free-hlmem.patch
  patch -p1 < ../procps-3.2.7-ps-man-fmt.patch
  patch -p1 < ../procps-3.2.7-psman.patch
  patch -p1 < ../procps-3.2.7-longcmd.patch
  patch -p1 < ../procps-3.2.7-slabtop-once.patch
  patch -p1 < ../procps-3.2.7-top-clrscr.patch
  patch -p1 < ../procps-3.2.7-top-cpu0.patch
  patch -p1 < ../procps-3.2.7-top-env-cpuloop.patch
  patch -p1 < ../procps-3.2.7-top-remcpu.patch
  patch -p1 < ../procps-3.2.7-top-sorthigh.patch
  patch -p1 < ../procps-3.2.7-watch-unicode.patch
  patch -p1 < ../procps-3.2.7-w-best.patch
  patch -p1 < ../procps-3.2.8+gmake-3.82.patch
  patch -p1 < ../procps-3.2.8-setlocale.patch
  patch -p1 < ../procps-3.2.8-threads.patch
  patch -p0 < ../procps-3.2.8-linux-ver-init.patch
  sed -i "s:\$(lib64):lib:" Makefile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" ldconfig=/bin/true install
  install -D -m644 "${srcdir}/sysctl.conf" "${pkgdir}/etc/sysctl.conf"
  install -d "${pkgdir}/usr/include/proc"
  install -m644 proc/*.h "${pkgdir}/usr/include/proc"
}

md5sums=('9532714b6846013ca9898984ba4cd7e0'
         '0948ac11925b28ea913cda6ac2be0241'
         '111f33c3fd8fab2fed36c279065fefff'
         '81ac7fb50fcdb03e57055c7b763bb275'
         'cb020bfd46689908b56bf3fc5a55828a'
         '8d74603be1e426037a1e2c2743ef7345'
         'bfb7530e248b86f1abf32bca09f28b68'
         '9a8c81aee89531b4a72f4e4e44fb055d'
         '511023a3f39a90aa6efcf77120d32eb6'
         'f29ef0b3a74767469651c9c335f09403'
         'd49673c19fa1a70f8e03fc1c24233458'
         '669955ca7fa7688e1c3ae7e723e0a567'
         'f77626450619b4bebbd67b9e164f1857'
         'c71b853144b4d22b861e435362845091'
         '105fbbf179e63b7ca960a2dd2e5fab5b'
         '7f2540d2d4a2a1a6af70e9a980835753'
         '8d1025ae906bf0320ad9bb5095f1b970'
         '2e6cce598f11d75becebbe58dbe9cbd4'
         'e2e5393d49034435c68469d008d56489'
         '6f46c8ea37c447a2d478883e89bf4d25')
