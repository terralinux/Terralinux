# $Id: PKGBUILD 100052 2010-11-20 03:28:20Z stephane $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=sed
pkgver=4.2.1
pkgrel=3
pkgdesc="GNU stream editor"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/sed"
license=('GPL3')
groups=('base')
depends=('acl' 'sh')
makedepends=('gettext')
install=sed.install
source=(ftp://ftp.gnu.org/pub/gnu/sed/${pkgname}-${pkgver}.tar.gz)
md5sums=('f0fd4d7da574d4707e442285fd2d3b86')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --bindir=/bin --without-included-regex
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=${pkgdir} install
}
